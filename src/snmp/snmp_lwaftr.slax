/* Copyright (c) 2016, Juniper Networks, Inc.
 * All rights reserved.
 */
version 1.0;

ns junos = "http://xml.juniper.net/junos/*/junos";
ns xnm = "http://xml.juniper.net/xnm/1.1/xnm";
ns jcs = "http://xml.juniper.net/junos/commit-scripts/1.0";
ns dyn = "http://exslt.org/dynamic";
ns snmp extension = "http://exslt.org/functions";

var $base-oid = '.1.3.6.1.4.1.2636.13.61.1.9.1.1';
var $last-oid = 2;
var $lwaftr = document("/tmp/stats.xml"); 

template get-value ( $instance, $oid) {

    var $full-oid = concat($base-oid, ".", $instance, ".", $oid);
    var $n = number($instance) + 1;

    if ($oid == "0") {
    
        <snmp-script-results> {
          <snmp-oid> $full-oid;
          <snmp-type> "string";
          <snmp-value> $lwaftr/snabb/instance[$n]/id;
        }

    } else if ($oid == "1") {

        <snmp-script-results> {
          <snmp-oid> $full-oid;
          <snmp-type> "integer";
          <snmp-value> $lwaftr/snabb/instance[$n]//rxpackets;
        }

    } else if ($oid == "2") {

        <snmp-script-results> {
          <snmp-oid> $full-oid;
          <snmp-type> "integer";
          <snmp-value> $lwaftr/snabb/instance[$n]//txpackets;
        }

    }
}

match / {

  var $snmp-action = snmp-script-input/snmp-action;
  var $snmp-oid = snmp-script-input/snmp-oid;
  var $suffix-oid-temp = substring-after($snmp-oid, $base-oid);

  /* extract instance and oid (if present) */ 
  var $suffix-oid = concat(substring-after($suffix-oid-temp, "."),".0");
  var $instance = substring-before($suffix-oid, '.');
  var $oid = substring-before(substring-after($suffix-oid, concat($instance,'.')),'.');

  expr jcs:syslog("external.info", "snmp-action=", $snmp-action, " instance=", $instance, " oid=", $oid);

  var $last-instance = count($lwaftr/snabb/instance);

  if ($snmp-action == 'get') {
    if ($oid == '' or $instance == '') {
      call get-value($instance = $instance, $oid = $oid);

    } else {
      call get-value($instance = $instance, $oid = $oid);
    }

  } else if ($snmp-action == 'get-next') {

      if ($instance == '' and $oid == '') {
        call get-value($instance = "0", $oid = "0");
      } else {
        if ($oid == '') {
          call get-value($instance = $instance, $oid = "0");
        } else {
          if ($oid == $last-oid) {
            var $next-instance = $instance + 1;
            if ($next-instance < $last-instance) {
              call get-value($instance = $next-instance, $oid = "0");
            }
          } else if (number($oid) < number($last-oid)) {
            var $next-oid = $oid + 1;
            call get-value($instance = $instance, $oid = $next-oid);
          }
        }
      }

  } else {
    expr jcs:syslog("external.error", "unknown snmp-action=", $snmp-action, " for instance=", $instance, " oid=", $oid);
  }

}
